# For platform-specific conditions
OS=`uname`

# Remember stuff
HISTSIZE=1000
SAVEHIST=$HISTSIZE
HISTFILE=~/.history

# Activate default virtual env. Checking $VIRTUAL_ENV does not work here since
# tmux sessions will get the env variable, but not the $PATH changes.
if [ -d $PIP_VIRTUALENV_BASE/default ]; then
  # This will modify the prompt, so do it before setting the prompt
  source $PIP_VIRTUALENV_BASE/default/bin/activate
fi

# Easier color output
autoload -U colors && colors

# Load function-based completion system
autoload -U compinit && compinit

# For Git info in prompt
autoload -Uz vcs_info

# Enable bash-like command line editing via shortcut (see binding below)
autoload -U edit-command-line

# Enable bash-like word killing that stops at slashes
autoload -U select-word-style

# Zsh Options {{{

# Change directory by typing its name like a command (useful with fzf)
setopt auto_cd

# Make cd push the old directory onto the stack
setopt auto_pushd

# Ignore duplicates in directory stack
setopt pushd_ignore_dups

# Compact match display
setopt list_packed

# More powerful pattern matching
setopt extended_glob

# Enable auto-correction
setopt correctall

# Ignore duplicates in history
setopt hist_ignore_all_dups

# Prevent command from going into history by prepending a space
setopt hist_ignore_space

# Don't wait until exiting the shell to write to history
setopt inc_append_history

# Share history with all other shells
setopt share_history

# Enable prompt expansion for nicer prompts
setopt prompt_subst

# /Options }}}

# Prompt {{{

# Only do VCS detection for Git
zstyle ':vcs_info:*' enable git
# Add yellow dot next to repo when there are staged changes
zstyle ':vcs_info:*' stagedstr ' %{%F{yellow}%}●%f'
# Add red dot next to repo when there are unstaged changes
zstyle ':vcs_info:*' unstagedstr ' %{%F{red}%}●%f'
zstyle ':vcs_info:*' check-for-changes true
# Used during an action, like rebase
zstyle ':vcs_info:*' actionformats '(%{%F{yellow}%}%b%f|%{%F{magenta}%}%a%f)'
# Normal prompt, just shows repo name in green
zstyle ':vcs_info:*' formats ' (%{%F{green}%}%b%f%c%u)'
precmd () {
  vcs_info
}

# user@host: in red in OSX, green otherwise
if [[ $OS == "Darwin" ]]; then
  PS1='%{%F{red}%}%n@%m%f:'
else
  PS1='%{%F{green}%}%n@%m%f:'
fi
# Current Time in 18:30 format
PS1='%{%F{magenta}%}[%T]%f '$PS1
# full directory name in yellow
PS1+='%{%F{yellow}%}%~%f'
# Include VCS info
PS1+='${vcs_info_msg_0_}'
# Number of suspended jobs, if >= 1
PS1+='%1(j. %{%F{cyan}%}[%j]%f . )'
# Spelling correction prompt
SPROMPT='zsh: correct %{%F{red}%}%R%f to %{%F{green}%}%r%f [nyae]? '

# /Prompt }}}

# Environment / Configuration {{{
# Keep emacs keys for easy readline
bindkey -e

# Use <C-x><C-e> like bash to open command line in $EDITOR
zle -N edit-command-line
bindkey '^xe' edit-command-line
bindkey '^x^e' edit-command-line

# <C-w> respects slash as a word
select-word-style bash

# Nicer colors. BSD uses $LSCOLORS, linux uses $LS_COLORS
export LSCOLORS=gxfxbxbxCxegedabagGxGx
export LS_COLORS='di=36;40:ln=35;40:so=31;40:pi=31;40:ex=1;32;40:bd=34;46:cd=34;43:su=0;41:sg=0;46:tw=36;01:ow=36;01:'

# Use same ls colors for completion, zsh uses linux-style colors
zstyle ':completion:*:default' list-colors ${(s.:.)LS_COLORS}
# }}}

# Load aliases
if [ -f $HOME/.aliases ]; then
  source $HOME/.aliases
fi

# Add fzf support, if present
if [ -f ~/.fzf.zsh ]; then
  source ~/.fzf.zsh
fi

# Load OS-specific files
if [ $OS = "Darwin" ]; then
  if [ -f ~/.zshrc.osx ]; then
    source ~/.zshrc.osx
  fi
elif [ $OS = "Linux" ]; then
  if [ -f ~/.zshrc.linux ]; then
    source ~/.zshrc.linux
  fi
fi

# Load local file if present
if [ -f ~/.zshrc.local ]; then
  source ~/.zshrc.local
fi
